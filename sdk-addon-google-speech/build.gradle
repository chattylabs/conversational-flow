buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.8.1'
    }
}
repositories {
    maven { url "https://dl.bintray.com/chattylabs/maven" }
}
apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.novoda.bintray-release'
apply from: '../buildsystem/android.gradle'

group = 'com.chattylabs.sdk.android.addon'
version = android.defaultConfig.versionName

android {
    //compileSdkVersion 21
}

ext {
    supportLibraryVersion = '25.4.0'
    grpcVersion = '1.4.0'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.3.0'
    }
    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
        }
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
                grpc {
                    // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Dagger
    api 'com.google.dagger:dagger:2.15'
    kapt 'com.google.dagger:dagger-compiler:2.15'

    // SDK
    api 'com.chattylabs.sdk.android:common:+'
    api project(':sdk-conversational-flow-core')

    // Google Cloud
    implementation "com.google.cloud:google-cloud-texttospeech:0.53.0-beta"

    // gRPC
    implementation "io.grpc:grpc-okhttp:$grpcVersion"
    implementation "io.grpc:grpc-protobuf-lite:$grpcVersion"
    implementation "io.grpc:grpc-stub:$grpcVersion"
    implementation 'javax.annotation:javax.annotation-api:1.2'
    protobuf 'com.google.protobuf:protobuf-java:3.4.0'

    // OAuth2 for Google API
    implementation ('com.google.auth:google-auth-library-oauth2-http:0.7.0') {
        exclude module: 'httpclient'
    }
}

publish {
    userOrg = 'chattylabs'
    groupId = project.group
    artifactId = project.name.replace("sdk-", "")
    publishVersion = project.version
    desc = 'Google Speech Addon for the Conversational Flow Component (VUI SDK)'
    website = 'https://github.com/chattylabs/sdk-conversational-flow'
}
