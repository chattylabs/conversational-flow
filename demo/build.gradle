apply plugin: 'com.android.application'
apply plugin: 'com.chattylabs.versioning'
apply from: '../android-buildsystem/android.gradle'
apply from: '../android-buildsystem/versioning.gradle'

versioning {

    tagPrefix "demo-version/"

    keywords {
        minor "[demo-feature]", "[feature]"
        patch "[demo-bug]", "[demo-patch]", "[patch]"
    }
}

android {

    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        applicationId "chattylabs.vui.demo"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Enable this option on your own gradle file if you need it
        vectorDrawables.useSupportLibrary = true

        manifestPlaceholders = [HOCKEYAPP_APP_ID: "658862d7f4d34d89b96ae8356aafdb05"]
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Android dependencies
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-rc01'

    // Dagger
    implementation 'com.google.dagger:dagger:2.16'
    implementation 'com.google.dagger:dagger-android:2.16'
    implementation 'com.google.dagger:dagger-android-support:2.16'
    kapt 'com.google.dagger:dagger-compiler:2.15'
    kapt 'com.google.dagger:dagger-android-processor:2.15'

    // Dependencies
    implementation 'net.hockeyapp.android:HockeySDK:5.1.0'
    implementation "com.chattylabs:android-commons:$android_commons"
    implementation project(':sdk-vui-core')
    implementation project(':sdk-addon-android-speech')
    implementation project(':sdk-addon-google-speech')
    implementation project(':sdk-addon-amazon-speech')
}

task copySecretKey(type: Copy) {
    // Search GOOGLE_APPLICATION_CREDENTIALS on internet and ADD the Env Variable on your Machine.
    File secretKey = file "$System.env.GOOGLE_APPLICATION_CREDENTIALS"
    if ((project.hasProperty("isCi") ? project.isCi : false) || !secretKey.exists()) {
        secretKey = File.createTempFile('temp', 'file')
        secretKey.deleteOnExit()
    }
    from secretKey.getParent()
    include secretKey.getName()
    into 'src/main/res/raw'
    rename secretKey.getName(), "google_credentials.json"
}
preBuild.dependsOn(copySecretKey)